{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "83811563",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "import Pkg\n",
    "Pkg.activate(@__DIR__)\n",
    "Pkg.instantiate()\n",
    "import MathOptInterface as MOI\n",
    "import Ipopt \n",
    "import FiniteDiff\n",
    "import ForwardDiff\n",
    "import Convex as cvx \n",
    "import ECOS\n",
    "using LinearAlgebra\n",
    "using Plots\n",
    "using Random\n",
    "using JLD2\n",
    "using Test\n",
    "import MeshCat as mc \n",
    "using Statistics "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "36b869bf",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "include(joinpath(@__DIR__, \"utils\",\"fmincon.jl\"))\n",
    "include(joinpath(@__DIR__, \"utils\",\"planar_quadrotor.jl\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cce7e616",
   "metadata": {},
   "source": [
    "# Q3: Quadrotor Reorientation (40 pts)\n",
    "\n",
    "In this problem, you will use the trajectory optimization tools you have demonstrated in questions one and two to solve for a collision free reorientation of three planar quadrotors. The planar quadrotor (as described in lecture 10) is described with the following state and dynamics: \n",
    "\n",
    "$$\\begin{align} x = \\begin{bmatrix} p_x \\\\ p_z \\\\ \\theta \\\\ v_x \\\\ v_z \\\\ \\omega \\end{bmatrix}, &\\quad \\quad  \\end{align} \\dot{x} = \\begin{bmatrix}v_x \\\\ v_z \\\\ \\omega \\\\ \\frac{1}{m}(u_1 + u_2)\\sin\\theta \\\\ \\frac{1}{m}(u_1 + u_2)\\cos\\theta \\\\ \\frac{\\ell}{2J}(u_2 - u_1)\\end{bmatrix} $$ \n",
    "\n",
    "where $p_x$ and $p_z$ are the horizontal and vertial positions, $v_x$ and $v_z$ are the corresponding velocities, $\\theta$ for orientation, $\\omega$ for angular velocity, $\\ell$ for length of the quadrotor, $m$ for mass, $g$ for gravity acceleration in the $-z$ direction, and a moment of inertia of $J$. \n",
    "\n",
    "You are free to use any solver/cost/constraint you would like to solve for three collision free, dynamically feasible trajectories for these quadrotors that looks something like the following:\n",
    "\n",
    "<div>\n",
    "<img src=\"attachment:quadrotor_reorient.gif\" width=\"500\"/>\n",
    "</div>\n",
    "\n",
    "(if an animation doesn't load here, check out `quadrotor_reorient.gif`.) \n",
    "\n",
    "Here are the performance requirements that the resulting trajectories must meet: \n",
    "\n",
    "- The three quadrotors must start at `x1ic`, `x2ic`, and `x2ic` as shown in the code (these are the initial conditions).\n",
    "\n",
    "- The three quadrotors must finish their trajectories within **.2** meters of `x1g`, `x2g`, and `x2g` (these are the goal states). \n",
    "\n",
    "- The three quadrotors must never be within **0.8** meters of one another (use $[p_x,p_z]$ for this).\n",
    "\n",
    "There are two main ways of going about this:\n",
    "\n",
    "1. **Cost Shaping**: Design cost functions for each quadrotor that motivates them to take paths that do not result in a collision. You can do something like designing a reference trajectory for each quadrotor to use in the cost. You can use iLQR or DIRCOL for this.  \n",
    "\n",
    "2. **Collision Constraints**: You can optimize over all three quadrotors at once by creating a new state $\\tilde{x} = [x_1^T,x_2^T,x_3^T]^T$ and control $\\tilde{u} = [u_1^T,u_2^T,u_3^T]^T$, and then directly include collision avoidance constraints.  In order to use constraints, you must use DIRCOL (at least for now). \n",
    "\n",
    "### Hints\n",
    "\n",
    "- You should not use `norm() >= R` in any constraints, instead you should square the constraint to be `norm()^2 >= R^2`. This second constraint is still non-convex, but it is differentiable everywhere. \n",
    "\n",
    "- If you are using DIRCOL, you can initialize the solver with a \"guess\" solution by linearly interpolating between the initial and terminal conditions. Julia let's you create a length N linear interpolated vector of vectors between `a::Vector` and `b::Vector` like this: `range(a, b, length = N)` (experiment with this to see how it works). \n",
    "\n",
    "You can use either RK4 (iLQR or DIRCOL) or Hermite-Simpson (DIRCOL) for your integration. The `dt = 0.2`, and `tf = 5.0` are given for you in the code (you may change these but only if you feel you really have to). "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ab47d415",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "function single_quad_dynamics(params, x,u)\n",
    "    # planar quadrotor dynamics for a single quadrotor \n",
    "\n",
    "    # unpack state\n",
    "    px,pz,θ,vx,vz,ω = x  \n",
    "    \n",
    "    xdot = [\n",
    "        vx,\n",
    "        vz,\n",
    "        ω,\n",
    "        (1/params.mass)*(u[1] + u[2])*sin(θ),\n",
    "        (1/params.mass)*(u[1] + u[2])*cos(θ) - params.g,\n",
    "        (params.ℓ/(2*params.J))*(u[2]-u[1])\n",
    "    ]\n",
    "    \n",
    "    return xdot\n",
    "end\n",
    "function combined_dynamics(params, x,u)\n",
    "    # dynamics for three planar quadrotors, assuming the state is stacked\n",
    "    # in the following manner: x = [x1;x2;x3]\n",
    "    \n",
    "    # NOTE: you would only need to use this if you chose option 2 where \n",
    "    # you optimize over all three trajectories simultaneously \n",
    "    \n",
    "    # quadrotor 1 \n",
    "    x1 = x[1:6]\n",
    "    u1 = u[1:2]\n",
    "    xdot1 = single_quad_dynamics(params, x1, u1)\n",
    "    \n",
    "    # quadrotor 2\n",
    "    x2 = x[(1:6) .+ 6]\n",
    "    u2 = u[(1:2) .+ 2]\n",
    "    xdot2 = single_quad_dynamics(params, x2, u2)\n",
    "    \n",
    "    # quadrotor 3\n",
    "    x3 = x[(1:6) .+ 12]\n",
    "    u3 = u[(1:2) .+ 4]\n",
    "    xdot3 = single_quad_dynamics(params, x3, u3)\n",
    "    \n",
    "    # return stacked dynamics \n",
    "    return [xdot1;xdot2;xdot3]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e2281545",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "function hermite_simpson(params::NamedTuple, x1::Vector, x2::Vector, u, dt::Real)::Vector\n",
    "    # TODO: input hermite simpson implicit integrator residual \n",
    "    ẋ1 = combined_dynamics(params, x1, u)\n",
    "    ẋ2 = combined_dynamics(params, x2, u)\n",
    "    xm = (1/2)*(x1 + x2) + (dt/8)*(ẋ1 - ẋ2)\n",
    "    ẋm = combined_dynamics(params, xm, u)\n",
    "    x1 + (dt/6)*(ẋ1 + 4*ẋm + ẋ2) - x2\n",
    "end\n",
    "\n",
    "function create_idx(nx,nu,N)\n",
    "    # This function creates some useful indexing tools for Z \n",
    "    # x_i = Z[idx.x[i]]\n",
    "    # u_i = Z[idx.u[i]]\n",
    "    \n",
    "    # Feel free to use/not use anything here.\n",
    "    \n",
    "    \n",
    "    # our Z vector is [x0, u0, x1, u1, …, xN]\n",
    "    nz = (N-1) * nu + N * nx # length of Z \n",
    "    x = [(i - 1) * (nx + nu) .+ (1 : nx) for i = 1:N]\n",
    "    u = [(i - 1) * (nx + nu) .+ ((nx + 1):(nx + nu)) for i = 1:(N - 1)]\n",
    "    \n",
    "    # constraint indexing for the (N-1) dynamics constraints when stacked up\n",
    "    c = [(i - 1) * (nx) .+ (1 : nx) for i = 1:(N - 1)]\n",
    "    nc = (N - 1) * nx # (N-1)*nx \n",
    "    \n",
    "    return (nx=nx,nu=nu,N=N,nz=nz,nc=nc,x= x,u = u,c = c)\n",
    "end\n",
    "\n",
    "function cost(params::NamedTuple, Z::Vector)::Real\n",
    "    idx, N, xg = params.idx, params.N, params.xg\n",
    "    Q, R, Qf = params.Q, params.R, params.Qf\n",
    "    \n",
    "    \n",
    "    J = 0 \n",
    "    for i = 1:(N-1)\n",
    "        xi = Z[idx.x[i]]\n",
    "        ui = Z[idx.u[i]]\n",
    "       \n",
    "        J += 0.5*(xi - xg)'*Q*(xi - xg)\n",
    "        J += 0.5*ui'*R*ui \n",
    "    end\n",
    "    \n",
    "    xn = Z[idx.x[N]]\n",
    "    J += 0.5*(xn - xg)'*Qf*(xn - xg)\n",
    "        \n",
    "    return J \n",
    "end\n",
    "\n",
    "function dynamics_constraints(params::NamedTuple, Z::Vector)::Vector\n",
    "    idx, N, dt = params.idx, params.N, params.dt\n",
    "        \n",
    "    # create c in a ForwardDiff friendly way (check HW0)\n",
    "    c = zeros(eltype(Z), idx.nc)\n",
    "    \n",
    "    for i = 1:(N-1)\n",
    "        xi = Z[idx.x[i]]\n",
    "        ui = Z[idx.u[i]] \n",
    "        xip1 = Z[idx.x[i+1]]\n",
    "        c[idx.c[i]] = hermite_simpson(params, xi, xip1, ui, dt)\n",
    "    end\n",
    "    return c \n",
    "end\n",
    "\n",
    "function equality_constraint(params::NamedTuple, Z::Vector)::Vector\n",
    "    N, idx, xic, xg = params.N, params.idx, params.xic, params.xg \n",
    "    [\n",
    "        #initial condition\n",
    "        Z[idx.x[1]] - xic;\n",
    "        \n",
    "        #goal condition\n",
    "        Z[idx.x[N]] - xg;\n",
    "\n",
    "        #dynamics constraints\n",
    "        dynamics_constraints(params, Z)\n",
    "    ]\n",
    "end\n",
    "\n",
    "function inequality_constraint(params, Z)\n",
    "    c = zeros(eltype(Z), params.N*3)\n",
    "    idx = params.idx\n",
    "    off = 0 \n",
    "    for i = 1:params.N\n",
    "        x1 = Z[idx.x[i][1:2]]\n",
    "        x2 = Z[idx.x[i][(1:2) .+ 6]]\n",
    "        x3 = Z[idx.x[i][(1:2) .+ 12]]\n",
    "        \n",
    "        c[(1:3) .+ off] = [\n",
    "            norm(x1 - x2)^2,\n",
    "            norm(x1 - x3)^2,\n",
    "            norm(x2 - x3)^2\n",
    "        ]\n",
    "        off += 3 \n",
    "    end\n",
    "    return c \n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    quadrotor_reorient\n",
    "\n",
    "Function for returning collision free trajectories for 3 quadrotors. \n",
    "\n",
    "Outputs:\n",
    "    x1::Vector{Vector}  # state trajectory for quad 1 \n",
    "    x2::Vector{Vector}  # state trajectory for quad 2 \n",
    "    x3::Vector{Vector}  # state trajectory for quad 3 \n",
    "    u1::Vector{Vector}  # control trajectory for quad 1 \n",
    "    u2::Vector{Vector}  # control trajectory for quad 2 \n",
    "    u3::Vector{Vector}  # control trajectory for quad 3 \n",
    "    t_vec::Vector\n",
    "    params::NamedTuple\n",
    "\n",
    "The resulting trajectories should have dt=0.2, tf = 5.0, N = 26\n",
    "where all the x's are length 26, and the u's are length 25. \n",
    "\n",
    "Each trajectory for quad k should start at `xkic`, and should finish near \n",
    "`xkg`. The distances between each quad should be greater than 0.8 meters at \n",
    "every knot point in the trajectory. \n",
    "\"\"\"\n",
    "function quadrotor_reorient(;verbose=true)\n",
    "    \n",
    "    # problem size \n",
    "    nx = 18 \n",
    "    nu = 6\n",
    "    dt = 0.2\n",
    "    tf = 5.0 \n",
    "    t_vec = 0:dt:tf \n",
    "    N = length(t_vec)\n",
    "\n",
    "    # LQR cost \n",
    "    Q = diagm(ones(nx))\n",
    "    R = 1*diagm(ones(nu))\n",
    "    Qf = 1*diagm(ones(nx))\n",
    "    \n",
    "    # indexing \n",
    "    idx = create_idx(nx,nu,N)\n",
    "    \n",
    "    # initial conditions and goal states \n",
    "    lo = 0.5 \n",
    "    mid = 2 \n",
    "    hi = 3.5 \n",
    "    x1ic = [-2,lo,0,0,0,0]  # ic for quad 1 \n",
    "    x2ic = [-2,mid,0,0,0,0] # ic for quad 2 \n",
    "    x3ic = [-2,hi,0,0,0,0]  # ic for quad 3 \n",
    "\n",
    "    #stack initial conditions \n",
    "    xic = [x1ic;x2ic;x3ic] \n",
    "\n",
    "    x1g = [2,mid,0,0,0,0]   # goal for quad 1 \n",
    "    x2g = [2,hi,0,0,0,0]    # goal for quad 2 \n",
    "    x3g = [2,lo,0,0,0,0]    # goal for quad 3 \n",
    "\n",
    "    #stack goal conditions\n",
    "    xg = [x1g;x2g;x3g]\n",
    "    \n",
    "    # load all useful things into params \n",
    "    # TODO: include anything you would need for a cost function (like a Q, R, Qf if you were doing an \n",
    "    # LQR cost)\n",
    "    params = (Q = Q, R = R, Qf = Qf,\n",
    "              x1ic=x1ic,\n",
    "              x2ic=x2ic,\n",
    "              x3ic=x3ic,\n",
    "              x1g = x1g,\n",
    "              x2g = x2g,\n",
    "              x3g = x3g,\n",
    "              xic = xic,\n",
    "              xg = xg,\n",
    "              dt = dt,\n",
    "              N = N,\n",
    "              idx = idx,\n",
    "              mass = 1.0, # quadrotor mass \n",
    "              g = 9.81,   # gravity \n",
    "              ℓ = 0.3,    # quadrotor length \n",
    "              J = .018)   # quadrotor moment of inertia \n",
    "    \n",
    "    \n",
    "    # primal bounds \n",
    "    x_l = -Inf*ones(idx.nz)\n",
    "    x_u =  Inf*ones(idx.nz)    \n",
    "    \n",
    "    # inequality constraint bounds\n",
    "    c_l = (0.8^2)*ones(3*params.N)\n",
    "    c_u = Inf*ones(3*params.N)\n",
    "\n",
    "    # initial guess \n",
    "    z0 = zeros(idx.nz)\n",
    "    x_initialize = range(xic, xg, length = N)\n",
    "    for i = 1:N \n",
    "        z0[idx.x[i]] .= x_initialize[i]\n",
    "    end\n",
    "    z0 += 0.01*randn(idx.nz)\n",
    "    \n",
    "    # choose diff type (try :auto, then use :finite if :auto doesn't work)\n",
    "    diff_type = :auto     \n",
    "    \n",
    "    Z = fmincon(cost,equality_constraint,inequality_constraint,\n",
    "                x_l,x_u,c_l,c_u,z0,params, diff_type;\n",
    "                tol = 1e-6, c_tol = 1e-6, max_iters = 10_000, verbose = verbose)\n",
    "\n",
    "\n",
    "    # pull the X and U solutions out of Z \n",
    "    X = [Z[idx.x[i]] for i = 1:N]\n",
    "    U = [Z[idx.u[i]] for i = 1:(N-1)]\n",
    "\n",
    "\n",
    "    # return the trajectories \n",
    "    x1 = [x[1:6]         for x in X]\n",
    "    x2 = [x[(1:6) .+ 6]  for x in X]\n",
    "    x3 = [x[(1:6) .+ 12] for x in X]\n",
    "    u1 = [u[(1:2)]       for u in U]\n",
    "    u2 = [u[(1:2) .+ 2]  for u in U]\n",
    "    u3 = [u[(1:2) .+ 4]  for u in U]\n",
    "        \n",
    "    return x1, x2, x3, u1, u2, u3, t_vec, params \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "806b8527",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "@testset \"quadrotor reorient\" begin \n",
    "    \n",
    "    X1, X2, X3, U1, U2, U3, t_vec, params  = quadrotor_reorient(verbose=true)\n",
    "    \n",
    "    \n",
    "    #---------------testing-----------------\n",
    "    # check lengths of everything \n",
    "    @test length(X1) == length(X2) == length(X3)\n",
    "    @test length(U1) == length(U2) == length(U3)\n",
    "    @test length(X1) == params.N \n",
    "    @test length(U1) == (params.N-1)\n",
    "    \n",
    "    # check for collisions \n",
    "    distances = [distance_between_quads(x1[1:2],x2[1:2],x3[1:2]) for (x1,x2,x3) in zip(X1,X2,X3)]\n",
    "    @test minimum(minimum.(distances)) >= 0.799\n",
    "    \n",
    "    # check initial and final conditions \n",
    "    @test norm(X1[1] - params.x1ic, Inf) <= 1e-3 \n",
    "    @test norm(X2[1] - params.x2ic, Inf) <= 1e-3 \n",
    "    @test norm(X3[1] - params.x3ic, Inf) <= 1e-3 \n",
    "    @test norm(X1[end] - params.x1g, Inf) <= 2e-1 \n",
    "    @test norm(X2[end] - params.x2g, Inf) <= 2e-1 \n",
    "    @test norm(X3[end] - params.x3g, Inf) <= 2e-1 \n",
    "    \n",
    "    # check dynamic feasibility \n",
    "    @test check_dynamic_feasibility(params,X1,U1)\n",
    "    @test check_dynamic_feasibility(params,X2,U2)\n",
    "    @test check_dynamic_feasibility(params,X3,U3)\n",
    "    \n",
    "    \n",
    "    #---------------plotting/animation-------\n",
    "    display(animate_planar_quadrotors(X1,X2,X3, params.dt))\n",
    "    \n",
    "    plot(t_vec, 0.8*ones(params.N),ls = :dash, color = :red, label = \"collision distance\",\n",
    "         xlabel = \"time (s)\", ylabel = \"distance (m)\", title = \"Distance between Quadrotors\")\n",
    "    display(plot!(t_vec, hcat(distances...)', label = [\"|r_1 - r_2|\" \"|r_1 - r_3|\" \"|r_2 - r_2|\"]))\n",
    "        \n",
    "    X1m = hcat(X1...)\n",
    "    X2m = hcat(X2...)\n",
    "    X3m = hcat(X3...)\n",
    "    \n",
    "    plot(X1m[1,:], X1m[2,:], color = :red,title = \"Quadrotor Trajectories\", label = \"quad 1\")\n",
    "    plot!(X2m[1,:], X2m[2,:], color = :green, label = \"quad 2\",xlabel = \"p_x\", ylabel = \"p_z\")\n",
    "    display(plot!(X3m[1,:], X3m[2,:], color = :blue, label = \"quad 3\"))\n",
    "    \n",
    "    plot(t_vec, X1m[3,:], color = :red,title = \"Quadrotor Orientations\", label = \"quad 1\")\n",
    "    plot!(t_vec, X2m[3,:], color = :green, label = \"quad 2\",xlabel = \"time (s)\", ylabel = \"θ\")\n",
    "    display(plot!(t_vec, X3m[3,:], color = :blue, label = \"quad 3\"))\n",
    "    \n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3512dffd",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.7",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
